apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

version = '16.0.0.1'
group = 'com.tomasznajda.rxrecaptcha'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "com.google.android.gms:play-services-safetynet:$playServicesVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"

    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:2.23.0"
    testImplementation("com.nhaarman:mockito-kotlin:1.4.0", {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
    })
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                groupId 'com.tomasznajda.rxrecaptcha'
                artifactId 'rxrecaptcha'

                name 'rx-recaptcha'
                description 'An easy way to use SafetyNet reCAPTCHA with RxJava2'
                url 'https://github.com/tomasznajda/rx-recaptcha'

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'tomasznajda'
                        name 'Tomasz Najda'
                        email 'tomasz.najda@hotmail.com'
                    }
                }
                scm {
                    connection 'https://github.com/tomasznajda/rx-recaptcha.git'
                    developerConnection 'https://github.com/tomasznajda/rx-recaptcha.git'
                    url 'https://github.com/tomasznajda/rx-recaptcha'

                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    excludes = ['**/*.kt']
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    if(project.rootProject.file('local.properties').exists()) {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())

        user = properties.getProperty("bintray.user")
        key = properties.getProperty("bintray.apikey")

        configurations = ['archives']
        override = true
        pkg {
            repo = 'rx-recaptcha'
            name = 'rx-recaptcha'
            desc = 'An easy way to use SafetyNet reCAPTCHA with RxJava2'
            websiteUrl = 'https://github.com/tomasznajda/rx-recaptcha'
            vcsUrl = 'https://github.com/tomasznajda/rx-recaptcha.git'
            licenses = ["Apache-2.0"]
            publish = true
        }
    }
}